(*
 * Modeller: Roberto Metere, Feb-Apr 2017
 * Modeled from: Adjustments to Hao and Shahandashti's patch
 * 
 * === Patched SPEKE protocol ===
 * Here we show that in two messages instead of four, yet two round efficiency
 * as the ISO 2017, we can achieve server-side only authentication.
 *)
set attacker = active.
set ignoreTypes = true.

free c: channel.

type host.
type session.
type key.
type exponent.
type group.

(* Honest parties *)
const A, B, C, D: host.

(* Table of pre-shared passwords *)
table passwords(host, host, group).

(*
 * Diffie-Hellman
 * The group is a cyclic group in the exponents
 *)
const pwAB : group [private].
fun exp(group, exponent): group.
equation forall x: exponent, y: exponent; exp(exp(pwAB , x), y) = exp(exp(pwAB , y), x).

(* The participants may want to use the shared key for simmetric encryption *)
type plaintext.
type ciphertext.
fun enc(key, plaintext): ciphertext.
reduc forall k: key, p: plaintext; dec(k, enc(k, p)) = p.
free message: plaintext [private].
let processUseSessionKey(k: key) = out(c, enc(k, message)).

(*
 * Declare additional (empty) processes.
 * Depending on the property to check, they will be overwritten
 * with non-empty process.
 *)
let processAgreement = 0.
let processDisclosePasswords = 0.

(* Pre-shared passwords *)
let processSetPasswords(g: group) =
  insert passwords(A, B, g);
  insert passwords(B, A, g);
  
  (*
   * In the case A shares the same password with multiple entities.
   * We remark that such a situation is degenerative and a mistake
   * made by the user; so any fallacy entiled by such behaviour
   * is not strictly imputable to the protocol design.
   *
   * WARNING: impersonation property check overwrites this process.
   *)
(*   insert passwords(A, C, g); *)
(*   insert passwords(C, A, g); *)
(*   insert passwords(B, C, g); *)
(*   insert passwords(C, B, g); *)
  
  processDisclosePasswords.

let processPasswords = processSetPasswords(pwAB).


(* Correcness when parties behave honestly *)
table tableIhcorr(host, host, session, group, exponent, key).
table tableRhcorr(host, host, session, group, exponent, key).
event hcorrIR    (host, host, session, group, exponent, key, 
                  host, host, session, group, exponent, key).

set attacker = passive. (* speed up things even if not really needed *)
query s: session, x: exponent, y: exponent;
  let X = exp(pwAB, x) in
    let Y = exp(pwAB, y) in
      let k = KDF(A, X, B, Y, s, exp(Y, x)) in
  event(hcorrIR(A, B, s, pwAB, x, k, A, B, s, pwAB, y, k)) ==> true = true.

(* The event with "expected" results can be raised only if the protocol is correct *)
let processAgreement =
  get tableIhcorr(a , b , s , h , x, k ) in
  get tableRhcorr(a', b', s', h', y, k') in
  event hcorrIR  (a , b , s , h , x, k , a', b', s', h', y, k').

  (* Key Derivation Function. *)
fun KDF(host, group, host, group, session, group): key.

(* Key reduction *)
fun KEY(group, group, session, group): key.
equation forall a: host, X: group, b: host, Y: group, s: session, kmaterial: group; KDF(a, X, b, Y, s, kmaterial) = KEY(X, Y, s, kmaterial).

(* Key confirmation function *)
fun KCF(key, host, host): bitstring.

(* Initiator I talking to responder R *)
let processInitiator(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=I, =R, h) in
    new x: exponent;
    let X = exp(h, x) in
      out(c, (I, X)); (* first communication *)
      in(c, (=R, Y': group, hash: bitstring));
      let k = KEY(X , Y', s, exp(Y', x)) in
        if KCF(k, I, R) = hash then
insert tableIhcorr(I, R, s, h, x, k);
          (* Use the key -- out of the protocol *)
          processUseSessionKey(k).

(* Responder R talking to initiator I *)
let processResponder(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=R, =I, h) in
    new y: exponent;
    let Y = exp(h, y) in
      in(c, (=I, X': group)); (* first communication *)
      let k = KEY(X', Y , s, exp(X', y)) in
insert tableRhcorr(I, R, s, h, y, k);
          out(c, (R, Y, KCF(k, I, R)));
          (* Use the key -- out of the protocol *)
          processUseSessionKey(k).



(*
 * This process captures the idea that all pre-shared password can be used in
 * multiple sessions, but only at most one initiator and at most one responder
 * are allowed in the same session.
 * The symmetric nature of the protocol, if respected, will be in the two
 * processes for the initiator and the responder, i.e. contemporary output to
 * the channel.
 *)
let processSPEKE =
  (new sid: session;
    out(c, sid); (* the sID is explicitly not a secret *)
    (
        (processInitiator(sid))
      | (processResponder(sid))
    )
  ).

(*
 * The idea is that at first, we setup some honest entities to
 * share some password, otherwise the would never engage the protocol.
 * Then we run the protocol for unbounded number of concurrent executions.
 * Finally we record events relating to some values that the entities
 * can agree on.
 *)
process
  (
      processPasswords
    | (!processSPEKE)
    | (!processAgreement)
  )
