(*
 * Modeller: Roberto Metere, Feb-Apr 2017
 * Modeled from: IEEE P1363.2: D26
 * 
 * === Standardised SPEKE protocol ===
 * The protocol originally computes the common group generator from a
 * pre-shared secret. It uses the group exponentiation to generate
 * the material to generate future session keys. The security of the
 * scheme is based on the hardness of the inverse operation, i.e. the
 * discrete logarithm.
 * IEEE P1363.2: D26 and ISO/IEC 11770-4 (2006) share the same symbolic
 * model even if they are slightly different. Differencies are highlighted
 * in the standards for the sake of comparison with other stantards.
 * The key confirmation message is a hash of previous sent messages,
 * the password, the key material, and constant octets.
 *)
set attacker = active.
set ignoreTypes = true.

free c: channel.

type host.
type session.
type key.
type exponent.
type group.

(* Honest parties *)
const A, B, C, D: host.

(* Table of pre-shared passwords *)
table passwords(host, host, group).

(*
 * Diffie-Hellman
 * The group is a cyclic group in the exponents
 *)
const pwAB : group [private].
fun exp(group, exponent): group.
equation forall x: exponent, y: exponent; exp(exp(pwAB , x), y) = exp(exp(pwAB , y), x).

(* The participants may want to use the shared key for simmetric encryption *)
type plaintext.
type ciphertext.
fun enc(key, plaintext): ciphertext.
reduc forall k: key, p: plaintext; dec(k, enc(k, p)) = p.
free message: plaintext [private].
let processUseSessionKey(k: key) = out(c, enc(k, message)).

(*
 * Declare additional (empty) processes.
 * Depending on the property to check, they will be overwritten
 * with non-empty process.
 *)
let processAgreement = 0.
let processDisclosePasswords = 0.

(* Pre-shared passwords *)
let processSetPasswords(g: group) =
  insert passwords(A, B, g);
  insert passwords(B, A, g);
  
  (*
   * In the case A shares the same password with multiple entities.
   * We remark that such a situation is degenerative and a mistake
   * made by the user; so any fallacy entiled by such behaviour
   * is not strictly imputable to the protocol design.
   *
   * WARNING: impersonation property check overwrites this process.
   *)
(*   insert passwords(A, C, g); *)
(*   insert passwords(C, A, g); *)
(*   insert passwords(B, C, g); *)
(*   insert passwords(C, B, g); *)
  
  processDisclosePasswords.

let processPasswords = processSetPasswords(pwAB).


(* Detecting additional attacks/flaws *)
event endRauthIImp(host, host, session, key).
event endIauthRImp(host, host, session, key).

(* Impersonation attacks - Only Alice "exists", can the attacker coerce her to end a protocol? *)
query a: host, b: host, s: session, k: key;
  event(endRauthIImp(a, b, s, k));
  event(endIauthRImp(a, b, s, k)).
(* This means NOT event occurs in any trace *)

(* B would never engage the protocol with A, because he cannot find a matching password *)
let processPasswords =
  insert passwords(A, B, pwAB);
  
  (*
   * In the case A shares the same password with multiple entities.
   * We remark that such a situation is degenerative and a mistake
   * made by the user; so any fallacy entiled by such behaviour
   * is not strictly imputable to the protocol design.
   *)
(*   insert passwords(A, C, pwAB); *)
(*   insert passwords(A, D, pwAB); *)
  
  0.
fun grp2key(group): key [typeConverter].
fun fe2osp(group): bitstring [typeConverter].

(* KDF reduction *)
fun KDF(host, group, host, group, session, group): key.
equation forall a: host, X: group, b: host, Y: group, s: session, kmaterial: group; KDF(a, X, b, Y, s, kmaterial) = grp2key(kmaterial).

(* Key Confirmation Function for last phase *)
const three: bitstring.
const four: bitstring.
fun KCF(bitstring, group, group, bitstring, group): bitstring.

(* Initiator I talking to responder R *)
let processInitiator(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=I, =R, h) in
    new x: exponent;
    let X = exp(h, x) in
      out(c, (I, X)); (* first communication *)
      in(c, (=R, Y': group));
      let kmaterial = exp(Y', x) in
        let k = grp2key(kmaterial) in
          let hhk = KCF(three, X, Y', fe2osp(kmaterial), h) in
            let hk = KCF(four, X, Y', fe2osp(kmaterial), h) in
              out(c, hhk);
              in(c, =hk);
event endRauthIImp(I, R, s, k);
              (* Use the key -- out of the protocol *)
              processUseSessionKey(k).

(* Responder R talking to initiator I *)
let processResponder(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=R, =I, h) in
    new y: exponent;
    let Y = exp(h, y) in
      out(c, (R, Y)); (* first communication *)
      in(c, (=I, X': group));
      let kmaterial = exp(X', y) in
        let k = grp2key(kmaterial) in
          let hhk = KCF(three, X', Y, fe2osp(kmaterial), h) in
            let hk = KCF(four, X', Y, fe2osp(kmaterial), h) in
              out(c, hk);
              in(c, =hhk);
event endIauthRImp(I, R, s, k);
              (* Use the key -- out of the protocol *)
              processUseSessionKey(k).



(*
 * This process captures the idea that all pre-shared password can be used in
 * multiple sessions, but only at most one initiator and at most one responder
 * are allowed in the same session.
 * The symmetric nature of the protocol, if respected, will be in the two
 * processes for the initiator and the responder, i.e. contemporary output to
 * the channel.
 *)
let processSPEKE =
  (new sid: session;
    out(c, sid); (* the sID is explicitly not a secret *)
    (
        (processInitiator(sid))
      | (processResponder(sid))
    )
  ).

(*
 * The idea is that at first, we setup some honest entities to
 * share some password, otherwise the would never engage the protocol.
 * Then we run the protocol for unbounded number of concurrent executions.
 * Finally we record events relating to some values that the entities
 * can agree on.
 *)
process
  (
      processPasswords
    | (!processSPEKE)
    | (!processAgreement)
  )
