(*
 * Modeller: Roberto Metere, Feb-Apr 2017
 * Modeled from: ISO/IEC 11770-4:2017
 * 
 * === Patched SPEKE protocol - ISO standard of 2017 ===
 * The protocol originally computes the common group generator from a
 * pre-shared secret. It uses the group exponentiation to generate
 * the material to generate future session keys. The security of the
 * scheme is based on the hardness of the inverse operation, i.e. the
 * discrete logarithm.
 * The key confirmation message is a hash of previous sent messages,
 * the password, the key material, the identities, and the session.
 *)
set attacker = active.
set ignoreTypes = true.

free c: channel.

type host.
type session.
type key.
type exponent.
type group.

(* Honest parties *)
const A, B, C, D: host.

(* Table of pre-shared passwords *)
table passwords(host, host, group).

(*
 * Diffie-Hellman
 * The group is a cyclic group in the exponents
 *)
const pwAB : group [private].
fun exp(group, exponent): group.
equation forall x: exponent, y: exponent; exp(exp(pwAB , x), y) = exp(exp(pwAB , y), x).

(* The participants may want to use the shared key for simmetric encryption *)
type plaintext.
type ciphertext.
fun enc(key, plaintext): ciphertext.
reduc forall k: key, p: plaintext; dec(k, enc(k, p)) = p.
free message: plaintext [private].
let processUseSessionKey(k: key) = out(c, enc(k, message)).

(*
 * Declare additional (empty) processes.
 * Depending on the property to check, they will be overwritten
 * with non-empty process.
 *)
let processAgreement = 0.
let processDisclosePasswords = 0.

(* Pre-shared passwords *)
let processSetPasswords(g: group) =
  insert passwords(A, B, g);
  insert passwords(B, A, g);
  
  (*
   * In the case A shares the same password with multiple entities.
   * We remark that such a situation is degenerative and a mistake
   * made by the user; so any fallacy entiled by such behaviour
   * is not strictly imputable to the protocol design.
   *
   * WARNING: impersonation property check overwrites this process.
   *)
(*   insert passwords(A, C, g); *)
(*   insert passwords(C, A, g); *)
(*   insert passwords(B, C, g); *)
(*   insert passwords(C, B, g); *)
  
  processDisclosePasswords.

let processPasswords = processSetPasswords(pwAB).


(* Correcness when parties behave honestly *)
table tableIhcorr(host, host, session, group, exponent, key).
table tableRhcorr(host, host, session, group, exponent, key).
event hcorrIR    (host, host, session, group, exponent, key, 
                  host, host, session, group, exponent, key).

set attacker = passive. (* speed up things even if not really needed *)
query s: session, x: exponent, y: exponent;
  let X = exp(pwAB, x) in
    let Y = exp(pwAB, y) in
      let k = KDF(A, X, B, Y, s, exp(Y, x)) in
  event(hcorrIR(A, B, s, pwAB, x, k, A, B, s, pwAB, y, k)) ==> true = true.

(* The event with "expected" results can be raised only if the protocol is correct *)
let processAgreement =
  get tableIhcorr(a , b , s , h , x, k ) in
  get tableRhcorr(a', b', s', h', y, k') in
  event hcorrIR  (a , b , s , h , x, k , a', b', s', h', y, k').

  (* Hash functions *)
fun KEY(session, group): key.
fun H(host, session, group): bitstring.

(* Key Confirmation Function for last phase *)
fun KCF(host, host, group, group, session, group, group): bitstring.

(*
 * Generating the key.
 * SID(MAX(s1, s2),MIN(s1, s2)).
 *)
fun SID(bitstring, bitstring): session.

(* KDF reduction *)
fun KDF(host, group, host, group, session, group): key.
equation forall a: host, X: group, b: host, Y: group, s: session, kmaterial: group; KDF(a, X, b, Y, s, kmaterial) = KEY(SID(H(a, s, X), H(b, s, Y)), kmaterial).

(* Initiator I talking to responder R *)
let processInitiator(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=I, =R, h) in
    new x: exponent;
    let X = exp(h, x) in
      out(c, (I, X)); (* first communication *)
      in(c, (=R, Y': group));
      let sI = H(I, s, X) in
        let sR = H(R, s, Y') in
          let sID = SID(sI, sR) in
            let kmaterial = exp(Y', x) in
              let k = KEY(sID, kmaterial) in
                let verifIR = KCF(I, R, X, Y', sID, kmaterial, h) in
                  let verifRI = KCF(R, I, Y', X, sID, kmaterial, h) in
                    out(c, verifIR);
                    in(c, =verifRI);
insert tableIhcorr(I, R, s, h, x, k);
                    (* Use the key -- out of the protocol *)
                    processUseSessionKey(k).

(* Responder R talking to initiator I *)
let processResponder(s: session) =
  (* This input round is only used to establish who's whom *)
  in(c, (I: host, R: host));
  (* A party cannot possibly know how to communicate to "unknown" parties *)
  get passwords(=R, =I, h) in
    new y: exponent;
    let Y = exp(h, y) in
      out(c, (R, Y)); (* first communication *)
      in(c, (=I, X': group));
        let sI = H(I, s, X') in
          let sR = H(R, s, Y) in
            let sID = SID(sI, sR) in
              let kmaterial = exp(X', y) in
                let k = KEY(sID, kmaterial) in
                  let verifIR = KCF(I, R, X', Y, sID, kmaterial, h) in
                    let verifRI = KCF(R, I, Y, X', sID, kmaterial, h) in
                      out(c, verifRI);
                      in(c, =verifIR);
insert tableRhcorr(I, R, s, h, y, k);
                      (* Use the key -- out of the protocol *)
                      processUseSessionKey(k).



(*
 * This process captures the idea that all pre-shared password can be used in
 * multiple sessions, but only at most one initiator and at most one responder
 * are allowed in the same session.
 * The symmetric nature of the protocol, if respected, will be in the two
 * processes for the initiator and the responder, i.e. contemporary output to
 * the channel.
 *)
let processSPEKE =
  (new sid: session;
    out(c, sid); (* the sID is explicitly not a secret *)
    (
        (processInitiator(sid))
      | (processResponder(sid))
    )
  ).

(*
 * The idea is that at first, we setup some honest entities to
 * share some password, otherwise the would never engage the protocol.
 * Then we run the protocol for unbounded number of concurrent executions.
 * Finally we record events relating to some values that the entities
 * can agree on.
 *)
process
  (
      processPasswords
    | (!processSPEKE)
    | (!processAgreement)
  )
